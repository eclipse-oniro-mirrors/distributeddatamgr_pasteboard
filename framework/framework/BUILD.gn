# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")
import("//build/ohos_var.gni")
import("//foundation/distributeddatamgr/pasteboard/pasteboard.gni")

group("build_module") {
  deps = [ ":pasteboard_framework" ]
}
config("module_public_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "${pasteboard_utils_path}/native/include",
    "include",
    "include/device",
  ]
}

ohos_shared_library("pasteboard_framework") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  sources = [
    "clip/clip_plugin.cpp",
    "clip/default_clip.cpp",
    "device/dev_profile.cpp",
    "device/distributed_module_config.cpp",
    "device/dm_adapter.cpp",
    "device/para_handle.cpp",
    "serializable/serializable.cpp",
  ]
  cflags = [ "-Wno-multichar" ]

  cflags_cc = [
    "-fvisibility=hidden",
    "-fstack-protector",
    "-D_FORTIFY_SOURCE=2",
    "-O2",
  ]

  configs = [ ":module_public_config" ]

  configs += [ "//third_party/cJSON:cJSON_config" ]

  deps = [ "//third_party/cJSON:cjson" ]

  defines = []

  public_configs = [ ":module_public_config" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "init:libbeget_proxy",
    "init:libbegetutil",
    "ipc:ipc_core",
  ]

  if (pb_device_info_manager_part_enabled) {
    external_deps += [
      "device_info_manager:distributed_device_profile_common",
      "device_info_manager:distributed_device_profile_sdk",
    ]
    defines += [ "PB_DEVICE_INFO_MANAGER_ENABLE" ]
  }

  if (pb_device_manager_part_enabled) {
    external_deps += [ "device_manager:devicemanagersdk" ]
    defines += [ "PB_DEVICE_MANAGER_ENABLE" ]
  }
  subsystem_name = "distributeddatamgr"
  part_name = "pasteboard"
}
